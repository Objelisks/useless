<html>
  <head>
    <meta charset=utf-8>
    <title>NON EVIL GEM</title>
    <style>
      body { margin: 0; }
      canvas { width: 700px; height: 384px }
    </style>
  </head>
  <body>
<script id="gem-vertex" type="vertex-shader">
varying vec2 vUv;
varying vec3 vPosition;
varying mat4 vRotation;

void main() {
	vUv = uv;
	vPosition = position;
	vRotation = modelMatrix;
	gl_Position = projectionMatrix * modelViewMatrix * vec4(vPosition, 1.0);
}
</script>
<script id="gem-fragment" type="fragment-shader">
const float EPSILON = 0.1;
const float E = 2.71828;
const float RT2 = 0.7071067;
const float PI = 3.1415;

uniform float iGlobalTime;
uniform mat4 camera;
varying vec3 vPosition;
varying mat4 vRotation;
varying vec2 vUv;

float noise(vec3 co){
    return fract(sin(dot(co.xyz ,vec3(12.9898,78.233,129.13545))) * 43758.5453);
}

// helper function to create a 3d rotation matrix.
mat3 rotateX(float angle) {
    float ca = cos(angle);
    float sa = sin(angle);
	return mat3(1, 0, 0,  0, ca, -sa,  0, sa, ca);
}

// helper function to create a 3d rotation matrix.
mat3 rotateY(float angle) {
    float ca = cos(angle);
    float sa = sin(angle);
	return mat3(ca, 0, sa,  0, 1, 0,  -sa, 0, ca);
}

/*
	This function returns the distance from a specified point (p)
	to the edge of a sphere centered on c with radius r.

	Think of this as a 3d gradient which is black (0.0) at the edge of the sphere and
	progressively brighter as you move away from the sphere. Inside the sphere, the
	distance is negative up to -r at the center.
*/
float distanceSphere(vec3 p, vec3 c, float r) {
	return length(p - c) - r;
}


// simple function to get the distance from a plane
float plane(vec3 p, vec4 n) {
    return dot(p,n.xyz) + n.w;
}

/*
	This function gives the distance to a diamond cut object.
	h.x specifies the side length, h.y specifies the height
*/
float distanceDiamond(vec3 p, vec2 h) {
    // object is symmetrical about x and z axes
	vec3 q = vec3(abs(p.x), abs(p.y), abs(p.z));
    
    // specify the points that make up the object in the positive octant
    vec3 p1 = vec3(1.0, 0., 0.);
    vec3 p2 = vec3(RT2, 0., RT2);
    vec3 p3 = vec3(0., 1.0, 0.);
    vec3 p4 = vec3(0., 0., 1.0);
    
    // get the plane equations as a vec4 by using cross product on the points
    vec4 s1 = vec4(normalize(cross(p2-p1, p3-p1)), length(p1)+h.x);
    vec4 s2 = vec4(normalize(cross(p4-p2, p3-p2)), length(p2)+h.x);
    
    // get the farthest distance to one of the planes
    // p.y - h.y is the flat top of the diamond on one side
    // the second max gives the distance to one of the sides
    float dist = max(p.y - h.y, max(plane(q, -s1), plane(q, -s2)));
	return dist;
}

float dirtMap(vec3 p) {
	// smudge
    float dirt = 1.0 - abs(-1.0+2.0*pow(noise(p * 5.0), 8.0));
    return dirt;
}

float detailMap(vec3 p) {
	// scratches
    //float scratches = noise(p * vec3(5.0, 50.0, 100.0));
    // chips
    float chips = 1.0 - abs(-1.0+2.0*pow(noise(p * 5.0), 8.0));
    return chips;
}

/*
	Scene is a function that combines all the other distance functions and modifications
	so that for any given point, it returns the closest distance to something
	in the complete scene.
*/
float scene(vec3 p) {
    float ft = iGlobalTime * 4.0 / PI;
    float dirt = dirtMap(p);
    float detail = detailMap(p);
 	return distanceDiamond(p, vec2(0.2, 0.4)) - (0.2+0.1*sin(ft)) * dirt * 1.0 - detail * 0.001;
}

/*
	Gets the normal direction at a given point in space.
	Compute the derivative of each spatial direction.
*/
vec3 getNormal(vec3 p) {
	vec3 n1 = vec3(
        scene(vec3(p.x + EPSILON, p.y, p.z)),
        scene(vec3(p.x, p.y + EPSILON, p.z)),
        scene(vec3(p.x, p.y, p.z + EPSILON))
    );
    return normalize(n1 - scene(p));
}

float diffuse(vec3 normal, vec3 lightRay, float intensity) {
    return clamp(dot(lightRay, normal) * intensity, 0.0, 1.0);
}

float specular(vec3 reflectAngle, vec3 lightRay, float shininess) {
    return pow(clamp(dot(reflectAngle, lightRay), 0.0, 1.0), shininess);
}

void main()
{
    // rays start from the camera
    vec3 rayOrigin = vec3(0., 0., 5.0);
    vec3 rayDirection = normalize(vec3(vUv*2.0-1.0, -1.0));
    
    // multiplying the vector by the matrix rotates it in space
    // think of this as rotating the camera around the origin point
    rayOrigin = (vRotation * camera * vec4(rayOrigin, 1.0)).xyz;
    rayDirection = normalize(vRotation * camera * vec4(rayDirection, 0.0)).xyz;
    
    float minStep = 0.25;
    float distance = minStep * 2.0;
    float t = 0.;
    const int MAX_STEPS = 30;
    
    // raymarching time!
    // start at the camera position and head towards the current pixel
    // (current pixel depends on fov)
    for(int i=0; i<MAX_STEPS; i++) {
        // each step, move ahead as far as we can
        // if we know that the closest object is distance away, we can move at least that far
     	  t += distance*0.95;
        // find the distance to the closest edge
        distance = scene(rayOrigin + t * rayDirection);
        if(distance < minStep) {
			    break;
        }
    }
    
    vec3 currentPos = rayOrigin + t * rayDirection;

    
    // if we've stepped all the way and we aren't close to the surface, we've probably gone past it
    // so default to background and skip lighting calculations
    if(distance > 1.0) {
		gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
        discard;
    }

    float detail = detailMap(currentPos);
    float dirt = dirtMap(vUv.xyy * 2.0);
    
    vec3 lightPos = (vRotation * camera * vec4(-10.0, 4.0, 10.0, 1.0)).xyz;
    vec3 lightColor = vec3(3.0, 0.0, 0.0);
    vec3 lightDiff = lightPos - currentPos;
    
    vec3 directionToLight = normalize(lightDiff);
	  float distanceToLight = length(lightDiff);
    
    vec3 normal = getNormal(currentPos);
    vec3 reflectAngle = reflect(rayDirection, normal);
    
    vec3 color = vec3(0.5, 0.1, 0.5);
    float diffuseFactor = diffuse(normal, directionToLight, 1.0);
    float specularFactor = specular(reflectAngle, directionToLight, 32.0*(dirt+0.5));
    float ambient = 0.3;
    
    vec3 final = (clamp(diffuseFactor-dirt*0.1, 0.0, 1.0)) * color +
        		(clamp(specularFactor-detail, 0.0, 1.0)) * lightColor +
        		vec3(ambient);
    
	gl_FragColor = vec4(final, 1.0);
}
</script>
<script src="../../scripts/three.js"></script>
<script type="application/javascript">
  /* global THREE*/
  var width = 700;
  var height = 384;
  
  var scene = new THREE.Scene();
  var camera = new THREE.PerspectiveCamera(75, width/height, 0.1, 1000);
  //var camera = new THREE.OrthographicCamera(-width/2, width/2, height/2, -height/2, 1, 1000);
  
  var renderer = new THREE.WebGLRenderer();
  renderer.setSize( width, height );
  document.body.appendChild( renderer.domElement );
  
  var plane = new THREE.PlaneGeometry(100, 100);
  var mat = new THREE.ShaderMaterial({
    uniforms: {
      iGlobalTime: {value: 0},
      iResolution: {value: new THREE.Vector2(width, height)},
      iMouse: {value: new THREE.Vector2()},
      iChannel0: {value: new THREE.TextureLoader().load('./tex12.png')},
      camera: {type: 'm4', value: camera.matrixWorld }
    },
    vertexShader: document.getElementById('gem-vertex').textContent,
    fragmentShader: document.getElementById('gem-fragment').textContent,
    side: THREE.DoubleSide
  });
  scene.add(new THREE.Mesh(plane, mat));
  camera.position.y = 5;
  camera.position.z = 10;
  camera.lookAt(new THREE.Vector3(0,0,0));
  
  function render() {
    mat.uniforms.iGlobalTime.value += 0.01;
    renderer.render(scene, camera);
    requestAnimationFrame(render);
  }
  requestAnimationFrame(render);
</script>
  </body>
</html>